buildscript {
    ext.kotlin_version = '1.0.2'
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'kotlin'
apply plugin: 'forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = "2.0"
group= "lingerloot" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "LingeringLoot-1.7.10"

configurations {
    shadow
    compile.extendsFrom shadow
}

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "run"
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

shadowJar {
    archiveName = tasks.jar.archiveName
    configurations = [project.configurations.shadow]
    relocate 'kotlin', 'lingerlootkot.kotlin'
    relocate 'org.jetbrains.annotations', 'lingerlootkot.jetbrains.annotations'
//    classifier ''
}

jar {
    configurations.shadow.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
}
jar.dependsOn shadowJar
jar.enabled = false

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
